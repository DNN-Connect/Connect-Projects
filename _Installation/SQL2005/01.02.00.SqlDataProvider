/******* TABLES *******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
IF NOT EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}Connect_Projects_Urls') AND type in (N'U'))
BEGIN
CREATE TABLE {databaseOwner}{objectQualifier}Connect_Projects_Urls(
 [UrlId] [int] IDENTITY(1,1) NOT NULL,
 [ProjectId] [int] NOT NULL,
 [Url] [nvarchar](500) NOT NULL,
 [Description] [nvarchar](1000) NULL,
 [UrlType] [int] NOT NULL,
 [IsDead] [bit] NOT NULL,
 [LastChange] [datetime] NULL,
 [LastChecked] [datetime] NULL,
 [Retries] [int] NULL,
 CONSTRAINT PK_{objectQualifier}Connect_Projects_Urls PRIMARY KEY CLUSTERED 
(
 [UrlId] ASC
)WITH (STATISTICS_NORECOMPUTE = OFF, IGNORE_DUP_KEY = OFF)
)
END
GO

IF NOT EXISTS (SELECT * FROM sys.foreign_keys WHERE object_id = OBJECT_ID(N'{databaseOwner}FK_{objectQualifier}Connect_Projects_Urls_Projects') AND parent_object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}Connect_Projects_Urls'))
ALTER TABLE {databaseOwner}{objectQualifier}Connect_Projects_Urls  WITH CHECK ADD  CONSTRAINT FK_{objectQualifier}Connect_Projects_Urls_Projects FOREIGN KEY([ProjectId])
REFERENCES {databaseOwner}{objectQualifier}Connect_Projects_Projects ([ProjectId])
ON DELETE CASCADE
IF  EXISTS (SELECT * FROM sys.foreign_keys WHERE object_id = OBJECT_ID(N'{databaseOwner}FK_{objectQualifier}Connect_Projects_Urls_Projects') AND parent_object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}Connect_Projects_Urls'))
ALTER TABLE {databaseOwner}{objectQualifier}Connect_Projects_Urls CHECK CONSTRAINT FK_{objectQualifier}Connect_Projects_Urls_Projects
GO

/******* MIGRATE DATA *******/
INSERT INTO {databaseOwner}{objectQualifier}Connect_Projects_Urls
([ProjectId]
,[Url]
,[UrlType]
,[IsDead]
,[LastChange]
,[LastChecked]
,[Retries])
SELECT
 p.ProjectId,
 p.Url1,
 0,
 0,
 NULL,
 GETDATE(),
 5
FROM
 {databaseOwner}{objectQualifier}Connect_Projects_Projects p
WHERE
 ISNULL(p.Url1, '') <> ''
GO

INSERT INTO {databaseOwner}{objectQualifier}Connect_Projects_Urls
([ProjectId]
,[Url]
,[UrlType]
,[IsDead]
,[LastChange]
,[LastChecked]
,[Retries])
SELECT
 p.ProjectId,
 p.Url2,
 0,
 0,
 GETDATE(),
 GETDATE(),
 5
FROM
 {databaseOwner}{objectQualifier}Connect_Projects_Projects p
WHERE
 ISNULL(p.Url2, '') <> ''
GO

/******* REMOVE OLD COLUMNS *******/
ALTER TABLE {databaseOwner}{objectQualifier}Connect_Projects_Projects
DROP COLUMN Url1
GO

ALTER TABLE {databaseOwner}{objectQualifier}Connect_Projects_Projects
DROP COLUMN Url2
GO

/******* VIEWS *******/
IF  EXISTS (SELECT * FROM sys.views WHERE object_id = OBJECT_ID(N'{databaseOwner}{objectQualifier}vw_Connect_Projects_Projects'))
DROP VIEW {databaseOwner}{objectQualifier}vw_Connect_Projects_Projects
GO

SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE VIEW {databaseOwner}{objectQualifier}vw_Connect_Projects_Projects
AS
SELECT
 p.*,
 lt.TypeDescription LicenseType,
 lt.ProjectColor,
 uc.DisplayName AS CreatedByUser,
 um.DisplayName AS LastModifiedByUser,
 (SELECT COUNT(*) FROM {databaseOwner}{objectQualifier}Connect_Projects_Urls u WHERE u.ProjectId=p.ProjectId AND u.IsDead=0) NrLiveLinks
FROM {databaseOwner}{objectQualifier}Connect_Projects_Projects p
 INNER JOIN {databaseOwner}{objectQualifier}Connect_Projects_LicenseTypes lt ON p.LicenseTypeId=lt.LicenseTypeId
 LEFT JOIN {databaseOwner}{objectQualifier}Users uc ON uc.UserID = p.CreatedByUserID
 LEFT JOIN {databaseOwner}{objectQualifier}Users um ON um.UserID = p.LastModifiedByUserID
GO

/******* NEW SCHEDULED TASK *******/
INSERT INTO {databaseOwner}[{objectQualifier}Schedule]
           ([TypeFullName]
           ,[TimeLapse]
           ,[TimeLapseMeasurement]
           ,[RetryTimeLapse]
           ,[RetryTimeLapseMeasurement]
           ,[RetainHistoryNum]
           ,[AttachToEvent]
           ,[CatchUpEnabled]
           ,[Enabled]
           ,[ObjectDependencies]
           ,[Servers]
           ,[CreatedByUserID]
           ,[CreatedOnDate]
           ,[LastModifiedByUserID]
           ,[LastModifiedOnDate]
           ,[FriendlyName])
     VALUES
           ('Connect.DNN.Modules.Projects.Services.UrlCheckService, CONNECT.DNN.MODULES.PROJECTS'
           ,1
           ,'d'
           ,2
           ,'h'
           ,20
           ,''
           ,0
           ,1
           ,''
           ,null
           ,-1
           ,GETDATE()
           ,-1
           ,GETDATE()
           ,'DNN Connect projects module url checker')
GO

